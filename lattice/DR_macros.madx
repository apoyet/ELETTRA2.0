! Hard edge wiggler definition
!**************************************

WigglerHardEdge(Nper,Bw,lw,Eo): macro = {
WIGGLERPEAKFIELD := Bw; ! Tesla
WIGGLERPERIOD := lw;
POLELENGTH := WIGGLERPERIOD/4.0;
WIGANGLE := 1.0027*POLELENGTH*WIGGLERPEAKFIELD/(3.356*Eo);

WIGPOLEPOS: RBEND, L=POLELENGTH, ANGLE=WIGANGLE;
WIGPOLENEG: RBEND, L=POLELENGTH, ANGLE=-WIGANGLE;
WIGHALFPOLEPOS: RBEND, L=POLELENGTH/2.0, ANGLE=WIGANGLE/2.0;
WIGHALFPOLENEG: RBEND, L=POLELENGTH/2.0, ANGLE=-WIGANGLE/2.0;
WIGDRIFT: DRIFT, L=POLELENGTH;

WigglerHardEdge: LINE=(WIGHALFPOLEPOS,WIGDRIFT,
	Nper*(WIGPOLENEG,WIGDRIFT,WIGPOLEPOS,WIGDRIFT),
	WIGPOLENEG,WIGDRIFT,WIGHALFPOLEPOS);
}
! Sinusoidal field wiggler (based on wiggler model of V67_short)
!**************************************

/***
WigglerSinusoidaloriginal: macro = {
Lpole=0.020264236728;
Bw=1.964805278724880671;
angwig=0.417364361255E-2;
!angwig/2=0.208682180627E-2;
!angwig/4=0.104341090314E-2;

WIGPOLEPOS: SBEND, L=Lpole, ANGLE=angwig, E1= 
angwig/2, E2=angwig/2;
WIGPOLENEG: SBEND, L=Lpole, ANGLE=-angwig, E1= 
-angwig/2, E2=-angwig/2;
WIGHALFPOLEPOS: SBEND, L=Lpole, ANGLE=angwig*0.5, E1= 
angwig/4, E2=angwig/4;
WIGDRIFT: DRIFT, L=0.236788163577E-2;

WigglerSinusoidalorig: LINE=(WIGDRIFT, WIGHALFPOLEPOS, 
WIGDRIFT, 39*(WIGDRIFT, WIGPOLENEG, WIGDRIFT, WIGDRIFT, WIGPOLEPOS, 
WIGDRIFT), WIGDRIFT, WIGPOLENEG, WIGDRIFT, WIGDRIFT, 
WIGHALFPOLEPOS, WIGDRIFT);
}
**/
WigglerSinusoidal(Nper,Bw,lw,Eo): macro = {
Ldrift=0.236788163577E-2;
!Ldrift=lw/8/2;
Lpole=lw/2-2*Ldrift;
Bs=Bw*lw/(PI*Lpole);
angwig=Lpole*0.2998*Bs/Eo;

WIGPOLEPOS: SBEND, L=Lpole, ANGLE=angwig, E1= 
angwig/2, E2=angwig/2;
WIGPOLENEG: SBEND, L=Lpole, ANGLE=-angwig, E1= 
-angwig/2, E2=-angwig/2;
WIGHALFPOLEPOS: SBEND, L=Lpole, ANGLE=angwig*0.5, E1= 
angwig/4, E2=angwig/4;
WIGDRIFT: DRIFT, L=Ldrift;

WigglerSinusoidal: LINE=(WIGDRIFT, WIGHALFPOLEPOS, 
WIGDRIFT, Nper*(WIGDRIFT, WIGPOLENEG, WIGDRIFT, WIGDRIFT, WIGPOLEPOS, 
WIGDRIFT), WIGDRIFT, WIGPOLENEG, WIGDRIFT, WIGDRIFT, 
WIGHALFPOLEPOS, WIGDRIFT);
}

MATCHTME(TME,qx,qy,KQFa,KQDa): macro = {
	MATCH,SEQUENCE=TME; 
	GLOBAL,q1=qx,q2=qy;
	VARY,NAME=KQFa,STEP=1E-6;
	VARY,NAME=KQDa,STEP=1E-6;
	!LMDIF,CALLS=500,TOLERANCE=1e-25;
	JACOBIAN,CALLS=30,TOLERANCE=1.e-25;
ENDMATCH;

};

MATCHFODOw(FODOW,qx,qy,KQFw,KQDw): macro = {
	MATCH,SEQUENCE=FODOW; 
	GLOBAL,q1=qx,q2=qy;
	VARY,NAME=KQFw,STEP=1E-6;
	VARY,NAME=KQDw,STEP=1E-6;
	!LMDIF,CALLS=500,TOLERANCE=1e-21;
	JACOBIAN,CALLS=30,TOLERANCE=1.e-25;
ENDMATCH;
};

MATCHLAG(RING,LGRF1): macro = {
match,sequence=RING;
   VARY,NAME=LGRF1,STEP=1.0e-6;
   constraint, RANGE=#E,T=0;
   weight,T=10000;
   jacobian, calls= 20, tolerance=1.E-22, bisec=3;
   LMDIF, CALLS=1000, TOLERANCE=1.E-22;
endmatch;
};

MATCHChromMADX(RING,KS2X2,KS2Y1): macro = {
match,sequence=RING;
	GLOBAL,DQ1=0,DQ2=0;
	VARY,NAME=KS2X2,STEP=1E-6;
	VARY,NAME=KS2Y1,STEP=1E-6;
	LMDIF,CALLS=500,TOLERANCE=1e-21;
	JACOBIAN,CALLS=30,TOLERANCE=1.e-21;
ENDMATCH;
}

PTCtwiss(nnn): macro = {
ptc_create_universe;
	ptc_create_layout,model=2,method=6,nst=2;
	PTC_SETSWITCH, fringe=false, debuglevel=1, nocavity=false,radiation=true;
	PTC_ALIGN;
	SELECT, flag=ptc_twiss,column=name,s,beta11,alfa11,mu1,beta22,alfa22,mu2,disp1,disp2,disp3,disp4;
	ptc_twiss,closed_orbit=true,no=6,icase=5,file="dr_ptc_twiss_nnn.tfs";
ptc_end;
}

MISALIGNMENTS(RING,k): macro = {
	select,flag=error,pattern=quadrupole;!class=quadrupole;
	ealign,dphi=k*tgauss(2.5);
}

filename(nn): macro = {
	twiss,chrom,sequence=RING,file=twiss_nn.tfs;
	SAVE,sequence=RING,file=seq_nn.seq;
}

callfilename(nnn): macro = {
	call,file="seq_nnn.seq";
	
}

renamefiles(nnn): macro = {
	system, "mv outtt out_nnn.dat";
	system, "cp emit.dat emit_nnn.dat";
	system, "cp Bwlw.dat Bwlw_nnn.dat";
	system, "cp RingSummParams.dat RingSummParams_nnn.dat";
}

LAYOUT(name): macro = {

        ptc_create_universe;
        ptc_create_layout,model=2,method=6,nst=1;
        PTC_SETSWITCH, fringe=false, debuglevel=0; !, nocavity=false,radiation=false;

        ptc_printframes, file="name.C", format=rootmacro;!this prints my visualisation with ROOT

        ptc_end;
};
